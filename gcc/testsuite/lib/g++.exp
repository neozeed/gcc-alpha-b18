# Copyright (C) 1992, 1993, 1994, 1995, 1996, 1997 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

# Please email any bugs, comments, and/or additions to this file to:
# bug-g++@prep.ai.mit.edu

# This file was written by Rob Savoye (rob@cygnus.com)
# Many modifications by Jeffrey Wheat (cassidy@cygnus.com)
# With modifications by Mike Stump <mrs@cygnus.com>.

#
# g++ support library routines
#

#
# GXX_UNDER_TEST is the compiler under test.
#


set gpp_compile_options ""

#
# g++_version -- extract and print the version number of the compiler
#
proc g++_version { } {
    global GXX_UNDER_TEST
    
    # ignore any arguments after the command
    set compiler [lindex $GXX_UNDER_TEST 0]
    
    # verify that the compiler exists
    if { [is_remote host] || [which $compiler] != 0 } then {
	set tmp [remote_exec host "$compiler -v"]
	set status [lindex $tmp 0];
	set output [lindex $tmp 1];
	regexp "version.*$" $output version
	if { $status == 0 && [info exists version] } then {
	    if [is_remote host] {
		clone_output "$compiler $version\n"
	    } else {
		clone_output "[which $compiler] $version\n"
	    }
	} else {
	    clone_output "Couldn't determine version of [which $compiler]\n"
	}
    } else {
	# compiler does not exist (this should have already been detected)
	warning "$compiler does not exist"
    }
}

#
# g++_init -- called at the start of each subdir of tests
#

proc g++_init { args } {
    global subdir
    global gpp_initialized
    global base_dir
    global tmpdir
    global libdir
    global gluefile;
    global objdir srcdir
    global ALWAYS_CXXFLAGS
    global CXXFLAGS
    global TOOL_EXECUTABLE TOOL_OPTIONS
    global GXX_UNDER_TEST

    if ![info exists GXX_UNDER_TEST] then {
	if [info exists TOOL_EXECUTABLE] {
	    set GXX_UNDER_TEST $TOOL_EXECUTABLE;
	} else {
	    if [is_remote host] {
		set GXX_UNDER_TEST [transform c++]
	    } else {
		set GXX_UNDER_TEST [findfile $base_dir/../xgcc "$base_dir/../xgcc -B$base_dir/../" [transform c++]]
	    }
	}
    }

    # Bleah, nasty. Bad taste.
    if [ishost "*-dos-*" ] {
	regsub "c\\+\\+" "$GXX_UNDER_TEST" "gcc" GXX_UNDER_TEST
    }

    if ![is_remote host] {
	if { [which $GXX_UNDER_TEST] == 0 } then {
	    perror "$GXX_UNDER_TEST does not exist"
	    exit 1
	}
    }
    if ![info exists tmpdir] {
	set tmpdir "/tmp"
    }

    if [info exists gluefile] {
	unset gluefile
    }

    if [target_info exists needs_status_wrapper] {
	if [target_info exists uses_underscores] {
	    set flags "additional_flags=-DUNDERSCORES"
	} else {
	    set flags ""
	}
	set result [target_compile ${libdir}/test-glue.c testglue.o object $flags];
	if { $result != "" } {
	    # Compile failed, but it's not really fatal. (might be later,
	    # tho.)
	    if [info exists gluefile] {
		unset gluefile;
	    }
	} else {
	    set gluefile "testglue.o"
	}
    }


    set ALWAYS_CXXFLAGS ""
    set CXXFLAGS ""
    # g++ is not multilib, so we specify "" for the -m options.
    if ![is_remote host] {

	# The null string is needed because libg++/libstdc++ is not currently
	# compiled with multilibs.
	set gccpath [get_multilibs ""]

	if { $gccpath != "" } {
	    if [file exists $gccpath/libg++] {
		lappend ALWAYS_CXXFLAGS "libdir=$gccpath/libg++"
	    }
	    if [file exists $gccpath/libstdc++] {
		lappend ALWAYS_CXXFLAGS "libdir=$gccpath/libstdc++"
	    }
	} else {
	    set libgpp [lookfor_file ${objdir} libg++];
	    if { $libgpp != "" } {
		lappend ALWAYS_CXXFLAGS "libdir=$libgpp"
	    }
	    set libstdcpp [lookfor_file ${objdir} libstdc++];
	    if { $libstdcpp != "" } {
		lappend ALWAYS_CXXFLAGS "libdir=$libstdcpp"
	    }
	}

	set std_include "[lookfor_file ${srcdir} libstdc++]";
	if { $std_include != "" } {
	    lappend ALWAYS_CXXFLAGS "incdir=$std_include"
	    lappend ALWAYS_CXXFLAGS "incdir=$std_include/stl"
	}
	set libg_include "[lookfor_file ${srcdir} libg++/src]";
	if { $libg_include != "" } {
	    lappend ALWAYS_CXXFLAGS "incdir=$libg_include"
	}
	lappend ALWAYS_CXXFLAGS "incdir=$base_dir/../include"

    }

    if [info exists TOOL_OPTIONS] {
	lappend ALWAYS_CXXFLAGS "additional_flags=$TOOL_OPTIONS";
    }

    verbose -log "ALWAYS_CXXFLAGS set to $ALWAYS_CXXFLAGS"

    verbose "g++ is initialized" 3
}


proc g++_target_compile { source dest type options } {
    global tmpdir;
    global gpp_compile_options
    global gluefile
    global ALWAYS_CXXFLAGS;
    global CXXFLAGS;
    global GXX_UNDER_TEST;

    if { [target_info needs_status_wrapper] != "" && [info exists gluefile] } {
	lappend options "libs=${gluefile}"
	if { [target_info uses_underscores] != "" } {
	    lappend options "ldflags=-Wl,-wrap,__exit -Wl,-wrap,_main -Wl,-wrap,_abort"
	} else {
	    lappend options "ldflags=-Wl,-wrap,exit -Wl,-wrap,main -Wl,-wrap,abort"
	}
    }

    lappend options "additional_flags=[libio_include_flags]"
    lappend options "compiler=$GXX_UNDER_TEST";

    set options [concat $options $gpp_compile_options]

    set options [concat $options "$ALWAYS_CXXFLAGS"];

    if [info exists CXXFLAGS] {
	set options [concat $options "{additional_flags=$CXXFLAGS}"];
    }

    return [target_compile $source $dest $type $options]
}

proc g++_exit { args } {
    global gluefile;

    if [info exists gluefile] {
	file_on_build delete $gluefile;
	unset gluefile;
    }
}

# If this is an older version of dejagnu (without runtest_file_p),
# provide one and assume the old syntax: foo1.exp bar1.c foo2.exp bar2.c.
# This can be deleted after the next dejagnu release.

if { [info procs runtest_file_p] == "" } then {
    proc runtest_file_p { runtests testcase } {
	if { $runtests != "" && [regexp "\[.\]\[cC\]" $runtests] } then {
	    if { [lsearch $runtests [file tail $testcase]] >= 0 } then {
		return 1
	    } else {
		return 0
	    }
	}
	return 1
    }
}

# Provide a definition of this if missing (delete after next dejagnu release).

if { [info procs prune_warnings] == "" } then {
    proc prune_warnings { text } {
	return $text
    }
}

# Utility used by mike-g++.exp and old-dejagnu.exp.
# Check the compiler(/assembler/linker) output for text indicating that
# the testcase should be marked as "unsupported".
#
# When dealing with a large number of tests, it's difficult to weed out the
# ones that are too big for a particular cpu (eg: 16 bit with a small amount
# of memory).  There are various ways to deal with this.  Here's one.
# Fortunately, all of the cases where this is likely to happen will be using
# gld so we can tell what the error text will look like.

proc ${tool}_check_unsupported_p { output } {
    if [regexp "(^|\n)\[^\n\]*: region \[^\n\]* is full" $output] {
	return "memory full"
    }
    return ""
}

proc ${tool}_option_help { } {
    send_user "--additional_options,OPTIONS\t\tUse OPTIONS to compile the testcase files. OPTIONS should be comma-separated."
}

proc ${tool}_option_proc { option } {
    if[regexp "^--additional_options," $option] {
	global gpp_compile_options
	regsub "--additional_options," $option "" option
	foreach x [split $option ","] {
	    lappend gpp_compile_options "additional_flags=$x"
	}
	return 1;
    } else {
	return 0
    }
}
